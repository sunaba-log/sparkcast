variable "environment" {
  type        = string
  description = "Environment name (e.g., dev, prod)."

  validation {
    condition     = contains(["dev", "prod"], var.environment)
    error_message = "environment must be one of: dev, prod."
  }
}

variable "system" {
  type        = string
  description = "System name for default labels（provider の default_labels 用）。実リソース名の接頭辞は automator_name_prefix / ui_name_prefix を使う（#72）。"
}

# --- 実リソース名の接頭辞（#72: podcast-* → sparkcast-* の段階移行） ---
# 未指定なら現行名を維持するため、tfvars を触らない限り plan は no-change。
# 切替は「安いもの（データを持たない）」→「データを持つもの」の順に段階実施する。
# 手順・検証・ロールバックは infra/docs/sparkcast-rename-runbook.md を参照。

variable "automator_name_prefix" {
  type        = string
  default     = "podcast-automator"
  description = "automator 系リソース名の接頭辞（GCS / Cloud Run Job / Scheduler / Workflows / Eventarc / Artifact Registry）。var.system とは独立（ラベル変更が名前に波及しないようにするため）。"
}

variable "ui_name_prefix" {
  type        = string
  default     = "podcast-ui"
  description = "ui 系リソース名の接頭辞（Cloud Run Service / Artifact Registry / Scheduler）。"
}

variable "org" {
  type        = string
  description = "Organization name for default labels."
  default     = "sunabalog"
}

variable "project_id" {
  type        = string
  description = "Google Cloud project ID."
}

variable "region" {
  type        = string
  description = "Default region for resources that require it."
  default     = "asia-northeast1"
}

variable "gcs_retention_days" {
  type        = number
  description = "Days to retain input audio objects before deletion. Omit or set null to disable lifecycle deletion."
  default     = null

  validation {
    condition     = var.gcs_retention_days == null || var.gcs_retention_days > 0
    error_message = "gcs_retention_days must be null (disabled) or a positive number."
  }
}

variable "gcs_force_destroy" {
  type        = bool
  description = "Allow Terraform to delete the input bucket even if it contains objects."
  default     = false
}

variable "gcs_cors_origins" {
  type        = list(string)
  description = "Browser origins allowed to upload MP3 files directly."
}


variable "discord_webhook_info_secret_name" {
  type        = string
  description = "Secret Manager secret name for Discord info webhook URL."
}

variable "discord_webhook_error_secret_name" {
  type        = string
  description = "Secret Manager secret name for Discord error webhook URL."
}

variable "cloudflare_access_key_id_secret_name" {
  type        = string
  description = "Secret Manager secret name for Cloudflare R2 access key id."
  default     = "cloudflare-access-key-id"
}

variable "cloudflare_secret_access_key_secret_name" {
  type        = string
  description = "Secret Manager secret name for Cloudflare R2 secret access key."
  default     = "cloudflare-secret-access-key"
}

variable "cloudflare_account_id" {
  type        = string
  description = "Cloudflare account ID that owns the R2 bucket."
}

variable "cloudflare_api_token" {
  type        = string
  description = "Cloudflare API token for the restapi provider (Realtime apps). Pass TF_VAR_cloudflare_api_token (same value as CLOUDFLARE_API_TOKEN)."
  sensitive   = true
  default     = ""
}

variable "cloudflare_zone_name" {
  type        = string
  description = "Cloudflare zone name (e.g., example.com) for the custom domain."
}

variable "r2_bucket_name" {
  type        = string
  description = "Cloudflare R2 bucket name for podcast assets."
}

variable "r2_key_prefix" {
  type        = string
  description = "Key prefix in the R2 bucket for uploaded files (empty for root)."
  default     = "sunabalog"
}

variable "r2_subdomain" {
  type        = string
  description = "Subdomain part for the custom domain (e.g., dev.podcast)."
}

variable "discord_webhook_agenda_secret_name" {
  type        = string
  description = "Secret Manager secret name for Discord agenda webhook URL."
}

variable "discord_bot_token_secret_name" {
  type        = string
  description = "Secret Manager secret name for Discord Bot Token (read-only, used for transcript channel access). Empty string disables the env injection."
  default     = ""
}

variable "discord_transcript_channel_id" {
  type        = string
  description = "Discord channel ID for meeting transcripts. Empty string disables transcript fetch and preserves fallback path."
  default     = ""
}

variable "podcast_id" {
  type        = string
  description = "Firestore podcast document ID. Injected as PODCAST_ID into the Cloud Run job."
}

variable "database_url_secret_name" {
  type        = string
  description = "Secret Manager secret containing the PostgreSQL DATABASE_URL."
}

variable "jev_api_key_secret_name" {
  type        = string
  description = "Secret Manager secret name for the Jev API key used by the AI director."
  default     = "jev-api-key"
}

variable "audit_trace_enabled" {
  type        = bool
  description = "Whether to enable Jev input/output audit trace log recording (#220)."
  default     = true
}

variable "audit_trace_retention_days" {
  type        = number
  description = "Days to retain Jev audit trace JSONL objects in GCS before deletion (#220). Default 90 days."
  default     = 90

  validation {
    condition     = var.audit_trace_retention_days > 0
    error_message = "audit_trace_retention_days must be a positive number."
  }
}

variable "audit_trace_prefix" {
  type        = string
  description = "Object prefix within GCS bucket for Jev audit trace JSONL logs (#220)."
  default     = "audit_traces"
}

variable "sns_schedule_offset_hours" {
  type        = number
  description = "Hours after episode processing to schedule the first SNS promotion. Default 1 hour."
  default     = 1
}

variable "manage_firestore_database" {
  type        = bool
  description = "Whether this Terraform stack creates and manages the default Firestore database."
  default     = false
}

variable "enable_promoter" {
  type        = bool
  description = "Whether to deploy the X auto-posting Cloud Run Job and Scheduler. Requires X API secrets to exist."
  default     = false
}

variable "x_api_key_secret_name" {
  type        = string
  description = "Secret Manager secret name for X API Key (Consumer Key)."
  default     = "x-api-key"
}

variable "x_api_secret_secret_name" {
  type        = string
  description = "Secret Manager secret name for X API Secret (Consumer Secret)."
  default     = "x-api-secret"
}

variable "x_access_token_secret_name" {
  type        = string
  description = "Secret Manager secret name for X Access Token."
  default     = "x-access-token"
}

variable "x_access_token_secret_secret_name" {
  type        = string
  description = "Secret Manager secret name for X Access Token Secret."
  default     = "x-access-token-secret"
}

variable "promoter_scheduler_cron" {
  type        = string
  description = "Execution frequency of the promoter (cron format)."
  default     = "0 * * * *"
}

# ---------------------------------------------------------------------------
# sparkcast-ui（Cloud Run Service）まわりの変数。
# DB 接続系の変数（cloud_sql_* / db_password_secret_id / db_name / db_user）は
# Supabase 移行と Cloud SQL 撤去（#90）で不要になったため削除済み。
# アプリは DATABASE_URL（var.database_url_secret_name）だけで接続する。
# ---------------------------------------------------------------------------
variable "app_service_account_id" {
  type        = string
  description = "podcast-ui アプリ実行用サービスアカウントの account_id"
}

variable "app_service_account_display_name" {
  type        = string
  description = "アプリ実行用サービスアカウントの表示名"
}

variable "cron_secret_id" {
  type        = string
  description = "cron エンドポイント保護用トークンの Secret Manager シークレット ID"
  default     = "cron-secret"
}

# Cloud Run サービスは gcloud（cd の ui デプロイ）がリビジョンを管理するため、
# TF が template（env）を変更するとリビジョン名衝突で失敗する。DB パスワードの
# 参照シークレットは live と一致させる必要があるため直接参照化せず変数で保持する
# （dev=db-password / prod=automator 管理シークレット）。
variable "custom_domain" {
  type        = string
  description = "Cloud Run に割り当てるカスタムドメイン（例: dev.sparkcast.sunabalog.com）"
}

variable "billing_account_id" {
  type        = string
  description = "Cloud Billing account ID that funds this project (dev / prod 共通)."
  default     = "018558-5DAE46-0B8F06"
}

variable "budget_amount_jpy" {
  type        = number
  description = "Monthly budget amount in JPY for the project budget alert."

  validation {
    condition     = var.budget_amount_jpy > 0
    error_message = "budget_amount_jpy must be a positive number."
  }
}

variable "backup_retention_days" {
  type        = number
  description = "Days to retain DB backup dumps in GCS before deletion (#90 Stage 4)."
  default     = 30

  validation {
    condition     = var.backup_retention_days > 0
    error_message = "backup_retention_days must be a positive number."
  }
}

variable "backup_bucket_name_override" {
  type        = string
  default     = null
  description = "DB バックアップ用 GCS バケット名の一時的な固定値（#72 Stage 6 の 2 段階 apply 用）。通常は null。"
}

variable "backup_bucket_force_destroy" {
  type        = bool
  default     = false
  description = "DB バックアップ用 GCS バケットの force_destroy。#72 のリネームでバケットを作り直す環境だけ true にする（prod は false のままダンプを rsync で移送すること）。"
}

variable "backup_scheduler_cron" {
  type        = string
  description = "Cron schedule (Asia/Tokyo) for the daily DB backup job."
  default     = "0 3 * * *"
}

variable "enable_guest_mode" {
  type        = bool
  description = "ゲストログイン（「ゲストとして試す」）機能の有効化フラグ（dev のみ true とする）"
  default     = false
}

variable "rate_limit_daily" {
  type        = string
  description = "1日あたりのレート制限回数"
  default     = ""
}

variable "rate_limit_hourly" {
  type        = string
  description = "1時間あたりのレート制限回数"
  default     = ""
}

variable "enable_recording" {
  type        = bool
  description = "ブラウザ収録ルーム（#166）の有効化フラグ。使えるのは admin と制限を解除したユーザーだけ（#174）。"
  default     = false
}

variable "realtime_hostname" {
  type        = string
  description = "収録ルームの Cloudflare Worker（apps/realtime）のホスト名。wrangler.jsonc の routes と揃える。"
  default     = ""
}
