# Cloud Run で動く podcast-ui 本体と、そのデプロイ基盤。
# イメージのビルド・デプロイは GitHub Actions（.github/workflows/）が行い、
# ここではサービス定義・レジストリを管理する。API 有効化は
# google_project_service.required（locals.required_services）に集約している。

resource "google_artifact_registry_repository" "sparkcast_ui" {
  project       = var.project_id
  location      = var.region
  repository_id = local.ui_name_prefix
  format        = "DOCKER"
  description   = "podcast-ui のアプリイメージ"

  depends_on = [google_project_service.required]
}

resource "google_cloud_run_v2_service" "sparkcast_ui" {
  project  = var.project_id
  location = var.region
  name     = "${local.ui_name_prefix}-${var.environment}"
  ingress  = "INGRESS_TRAFFIC_ALL"

  # provider の既定は true。他の Cloud Run リソース（job.tf / agenda.tf / promoter.tf /
  # backup.tf / workflows.tf）と同様に false を明示する。true のままだと改名などの
  # replace が「cannot destroy service without setting deletion_protection=false」で
  # 失敗する（#72 Stage 7 で実際に発生）。
  deletion_protection = false

  template {
    service_account = google_service_account.app.email

    scaling {
      min_instance_count = 0
      max_instance_count = 4
    }

    containers {
      # 初回 apply 用のプレースホルダ。実イメージは GitHub Actions がデプロイする。
      image = "us-docker.pkg.dev/cloudrun/container/hello"

      ports {
        container_port = 8080
      }

      env {
        name  = "GOOGLE_CLOUD_PROJECT"
        value = var.project_id
      }
      # Supabase(Postgres) へ DATABASE_URL で接続する。
      # CLOUD_SQL_INSTANCE_CONNECTION_NAME を渡さないことで db-pool.ts の
      # DATABASE_URL 経路（discrete params + SSL）に載る。
      env {
        name = "DATABASE_URL"
        value_source {
          secret_key_ref {
            secret  = data.google_secret_manager_secret.database_url.secret_id
            version = "latest"
          }
        }
      }
      env {
        name  = "GCS_UPLOAD_BUCKET"
        value = google_storage_bucket.input.name
      }
      env {
        name  = "GCS_SIGNED_URL_TTL_SECONDS"
        value = "900"
      }
      env {
        name = "CRON_SECRET"
        value_source {
          secret_key_ref {
            secret  = data.google_secret_manager_secret.cron_secret.secret_id
            version = "latest"
          }
        }
      }
      dynamic "env" {
        for_each = var.enable_guest_mode ? [1] : []
        content {
          name  = "ENABLE_GUEST_MODE"
          value = "true"
        }
      }
      dynamic "env" {
        for_each = var.rate_limit_daily != "" ? [1] : []
        content {
          name  = "RATE_LIMIT_DAILY"
          value = var.rate_limit_daily
        }
      }
      dynamic "env" {
        for_each = var.rate_limit_hourly != "" ? [1] : []
        content {
          name  = "RATE_LIMIT_HOURLY"
          value = var.rate_limit_hourly
        }
      }
      # ブラウザ収録ルーム（#166）。enable_recording = false の環境（prod）では何も足さない。
      dynamic "env" {
        for_each = var.enable_recording ? {
          RECORDING_ENABLED = "true"
          REALTIME_BASE_URL = "https://${var.realtime_hostname}"
          MIXER_JOB_NAME    = "projects/${var.project_id}/locations/${var.region}/jobs/${local.recording_mixer_job_name}"
        } : {}
        content {
          name  = env.key
          value = env.value
        }
      }
      dynamic "env" {
        for_each = var.enable_recording ? {
          RECORDING_ROOM_SECRET    = google_secret_manager_secret.recording_room_secret[0].secret_id
          RECORDING_SERVICE_SECRET = google_secret_manager_secret.recording_service_secret[0].secret_id
        } : {}
        content {
          name = env.key
          value_source {
            secret_key_ref {
              secret  = env.value
              version = "latest"
            }
          }
        }
      }
    }
  }

  # デプロイ（イメージ更新・リビジョン名・タグ付きプレビュー・トラフィック）は
  # GitHub Actions（gcloud）が行うため、terraform は初期作成のみ担い以降は無視する。
  # provider の default_labels 由来のサービスラベル更新も、gcloud 管理サービスへの
  # 不要な PATCH（リビジョン運用と競合し得る）を避けるため無視する。
  lifecycle {
    ignore_changes = [
      # 実イメージは CD の gcloud run deploy が配信するため TF は関与しない。
      # リソース定義のイメージは初回 apply 用のプレースホルダ。
      #
      # ⚠️ #72 Stage 7 で一時的にこれを外した経緯がある。Artifact Registry を
      # 作り直した際に state が持つイメージが実在しなくなり、ignore したまま TF が
      # それを再送して "Image not found" で更新に失敗したため。外して
      # プレースホルダへ収束させたあと、ここで元に戻している。
      # 外したままだと infra apply のたびにアプリがプレースホルダへ巻き戻る。
      template[0].containers[0].image,
      # gcloud が付けたリビジョン名を TF が管理しないようにする。
      #
      # ⚠️ ただし ignore したままだとその名前が state に取り込まれ、TF が
      # template（env 等）を変更する際に「同名リビジョンを別 config で再送」して
      # 409 になる（#90 Stage 2 / #72 Stage 7 で実際に発生）。
      # TF 側から env や service_account を変更する必要が生じたときは、
      # 一時的にここを外して Cloud Run に自動採番させること
      # （#72 Stage 8 の SA 改名では実際にそうした）。
      # ⚠️ #166: 収録用の env を足すため一時的に外している。dev への反映後に戻すこと。
      # template[0].revision,
      template[0].labels,
      template[0].annotations,
      # default_labels によるサービスラベル更新を抑止（gcloud 管理サービスへの不要 PATCH 回避）。
      # 「redundant」警告が出ても labels だけでは default_labels を止められないため両者を無視する。
      labels,
      terraform_labels,
      effective_labels,
      annotations,
      traffic,
      client,
      client_version,
    ]
  }

  depends_on = [
    google_project_service.required,
    google_secret_manager_secret_iam_member.app_secrets,
    google_secret_manager_secret_iam_member.app_recording_secrets,
  ]
}

# 管理画面はアプリ側の Firebase Auth で保護するため、HTTP は公開する。
#
# ⚠️ このバインディングの操作には run.services.setIamPolicy が必要で、共有デプロイ SA が
# 持つ editor には含まれない。ui_github_actions.tf の shared_deployer_run_admin で
# 付与済み（#72 Stage 7）。付与を消すとサービスの作り直しができなくなる。
resource "google_cloud_run_v2_service_iam_member" "public" {
  project = var.project_id
  # ドメイン制限共有の解除（下記 org policy）が先に必要
  location = google_cloud_run_v2_service.sparkcast_ui.location
  name     = google_cloud_run_v2_service.sparkcast_ui.name
  role     = "roles/run.invoker"
  member   = "allUsers"

  # ⚠️ ここに shared_deployer_run_admin への depends_on を足してはいけない。
  # 付与の create が、失敗し得る旧バインディングの destroy と同じ依存鎖に載り、
  # destroy が失敗すると付与まで実行されなくなる（#72 で実際に発生）。
  depends_on = [google_org_policy_policy.allowed_policy_member_domains]
}

output "cloud_run_uri" {
  description = "Cloud Run サービスの URL"
  value       = google_cloud_run_v2_service.sparkcast_ui.uri
}

# 組織のドメイン制限共有ポリシーの下では allUsers への権限付与ができないため、
# この プロジェクトに限り制限を解除する（公開 Web アプリの要件）。
resource "google_org_policy_policy" "allowed_policy_member_domains" {
  name   = "projects/${var.project_id}/policies/iam.allowedPolicyMemberDomains"
  parent = "projects/${var.project_id}"

  spec {
    inherit_from_parent = false

    rules {
      allow_all = "TRUE"
    }
  }

  depends_on = [google_project_service.required]
}
